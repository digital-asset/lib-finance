-- Copyright (c) 2019, Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

daml 1.2
module DA.Trigger.Finance.Instrument.Equity.Stock where

import DA.Foldable as F (forA_)
import DA.Next.Map (Map)
import DA.Next.Set as Set (singleton)
import Daml.Trigger

import DA.Finance.Asset.Lifecycle
import DA.Finance.Instrument.Equity.CashDividend
import DA.Finance.Instrument.Equity.StockSplit
import DA.Finance.Instrument.Equity.Stock.Lifecycle
import DA.Finance.Types
import DA.Trigger.Finance.Utils

-- | HIDE
ruleImpl : RelTime -> (Id -> Text) -> Party -> ACS -> Time -> Map CommandId [Command] -> () -> TriggerA ()
ruleImpl settlementOffset toEntitlementIdLabel ctrl acs currentTime _ _ = do
  let leData = map snd $ getContracts @LifecycleEffects acs
  let dividends = filter (dividendIsDue ctrl settlementOffset currentTime leData . snd) $ getContracts @EquityCashDividend acs
  let stockSplits = filter (stockSplitIsDue ctrl settlementOffset currentTime leData . snd) $ getContracts @EquityStockSplit acs

  forA_ dividends $ \(divCid, divData) -> do
    debug (show ctrl <> ": Processing " <> show divData.id)
    let optCmd = exerciseByKeyIfExistsCmd @EquityStockCashDividendRule acs divData.id.signatories
                    EquityStockCashDividend_Lifecycle with
                      dividendCid = divCid
                      entitlementIdLabel = toEntitlementIdLabel divData.id
    
    whenSomeNote (show ctrl <> ": `EquityStockCashDividendRule` contract " <> show divData.id <> " not found.") optCmd $ \cmd -> do
      () <$ emitCommands [ cmd ] [ toAnyContractId divCid ]

  forA_ stockSplits $ \(splitCid, splitData) -> do
    debug (show ctrl <> ": Processing " <> show splitData.id)
    let optCmd = exerciseByKeyIfExistsCmd @EquityStockSplitRule acs splitData.id.signatories
                    EquityStockSplit_Lifecycle with stockSplitCid = splitCid

    whenSomeNote (show ctrl <> ": `EquityStockSplitRule` contract " <> show splitData.id <> " not found.") optCmd $ \cmd -> do
      () <$ emitCommands [ cmd ] [ toAnyContractId splitCid ]

-- | HIDE
dividendIsDue : Party -> RelTime -> Time -> [LifecycleEffects] -> EquityCashDividend -> Bool
dividendIsDue ctrl settlementOffset currentTime leData EquityCashDividend{..} =
  all (\le-> le.id /= id) leData
  && addRelTimeToDate settlementOffset exDate <= currentTime
  && id.signatories == Set.singleton ctrl

-- | HIDE
stockSplitIsDue : Party -> RelTime -> Time -> [LifecycleEffects] -> EquityStockSplit -> Bool
stockSplitIsDue ctrl settlementOffset currentTime leData EquityStockSplit{..} =
  all (\le-> le.id /= id) leData
  && addRelTimeToDate settlementOffset exDate <= currentTime
  && id.signatories == Set.singleton ctrl

-- | A trigger that eagerly processes equity lifecycle event contracts
-- (`EquityCashDividend`, `EquityStockSplit`) once they are due.
--
-- Inputs:
--
-- `settlementOffset` (RelTime): Offset to the ex date when the lifecycle event
-- is processed.
--
-- `heartbeat` (RelTime): The heartbeat of the trigger.
--
-- `toEntitlementIdLabel` (Id -> Text): A function to derive the entitlement label
-- from the id of the lifecycle event (e.g. for `EquityCashDividend`).
trigger : RelTime -> RelTime -> (Id -> Text) -> Trigger ()
trigger settlementOffset heartbeat toEntitlementIdLabel =
  Trigger with
    initialize = const ()
    updateState = \_ _ s -> s
    heartbeat = Some heartbeat
    rule = ruleImpl settlementOffset toEntitlementIdLabel
    registeredTemplates = RegisteredTemplates
                            [ registeredTemplate @EquityCashDividend
                            , registeredTemplate @EquityStockCashDividendRule
                            , registeredTemplate @EquityStockSplit
                            , registeredTemplate @EquityStockSplitRule
                            , registeredTemplate @LifecycleEffects
                            ]
